plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.matthewprenger.cursegradle"
}
architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        dataGen {
            mod "minitect"
        }
    }
    launches {
        data {
            arg "--existing", file("../common/src/main/resources").absolutePath
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"
    modApi "me.shedaniel.cloth:cloth-config-forge:${rootProject.cloth_version}"

    modImplementation "com.withertech:mine_flux-forge:${rootProject.flux_version}"
    modImplementation "com.withertech:mine_tags-forge:${rootProject.tags_version}"
    modImplementation "com.withertech:mine_gui-forge:${rootProject.gui_version}"

    modImplementation "me.shedaniel:RoughlyEnoughItems-forge:${rootProject.rei_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

processResources {
    filesMatching("META-INF/mods.toml") {
        expand(rootProject.properties)
    }
}

shadowJar {
    exclude "fabric.mod.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}
publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java

            pom.withXml {
                asNode().dependencies.dependency.each { dep ->
                    if(dep.artifactId.last().value().last() in ["RoughlyEnoughItems-forge"]) {
                        assert dep.parent().remove(dep)
                    }
                }
            }
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        if (System.getenv("MAVEN_PASS") != null) {
            maven {
                name = "WitherTech"
                url = "http://maven.withertech.com/repository/withertech/"
                allowInsecureProtocol = true
                credentials {
                    username = "admin"
                    password = System.getenv("MAVEN_PASS")
                }
            }
        }
    }
}

curseforge {
    if (System.getenv("CURSE_API_KEY") != null) {
        apiKey = System.getenv("CURSE_API_KEY")
        project {
            id = "570667"
            releaseType = "$rootProject.cf_type"
            changelogType = "html"
            changelog = releaseChangelog()
            addGameVersion "1.18.1"
            addGameVersion "1.18"
            addGameVersion "1.18-Snapshot"
            addGameVersion "Java 17"
            addGameVersion "Forge"
            relations {
                requiredDependency "architectury-forge"
                requiredDependency "cloth-config-forge"
                requiredDependency "mine-flux-api"
                requiredDependency "mine-tags-api"
                requiredDependency "mine-gui-api"
            }
            mainArtifact(remapJar.archivePath) {
                displayName = "[Forge $rootProject.minecraft_version] v$project.version"
            }
            afterEvaluate {
                uploadTask.dependsOn("build")
            }
        }
        options {
            forgeGradleIntegration = false
            javaVersionAutoDetect = false
        }
    }
}
rootProject.tasks.getByName("curseforgePublish").dependsOn tasks.getByName("curseforge")

task decompile {
    dependsOn rootProject.tasks.getByName("decompile")
}